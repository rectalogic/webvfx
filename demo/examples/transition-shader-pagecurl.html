<html>
<head>
<script type="text/javascript" src="shader.js"></script>

<script type="text/javascript">
function PageCurl() {
    this.sourceImage = new Image();
    this.targetImage = new Image();
    this.shader = new WebVfx.Shader(document.getElementById("canvas"),
                                    WebVfx.Shader.loadShader("pageCurl"));
    var cz = this;
    this.sourceImage.onload = function() { cz.shader.updateUniform("sourceTex", cz.sourceImage); cz.shader.render(); }
    this.targetImage.onload = function() { cz.shader.updateUniform("targetTex", cz.targetImage); cz.shader.render(); }
    this.sourceImage.src = "http://evanw.github.com/webgl-filter/samples/Flowers.jpg";
    this.targetImage.src = "http://evanw.github.com/webgl-filter/samples/Wolf.jpg";
}

PageCurl.prototype.updateTime = function (time) {
    this.shader.updateUniform("time", time);
    this.shader.render();
}


var pageCurl;
function init() {
    pageCurl = new PageCurl();
}

window.addEventListener("load", init, false);
</script>


<!--
PageCurl fragment shader is based on code from http://labs.calyptus.eu/pagecurl/
Copyright (c) 2010 Calyptus Life AB <http://calyptus.eu>
And is licensed under The MIT License:
http://www.opensource.org/licenses/mit-license.php
-->
<script id="pageCurl" type="x-shader/x-fragment">
precision mediump float;
precision mediump sampler2D;

uniform sampler2D frame;
uniform vec2 size;
uniform float amount;

float PI = 3.141592;
float radius = 1.0 / PI / 2.0;

float scale = 512.0;

float sharpness = 3.0;

float cylinderCenter(){
	return amount;
}

float cylinderAngle(){
	return radians(360.0 * amount);
}

vec4 antiAlias(vec4 color1, vec4 color2, float distance)
{
	distance *= scale;
	if (distance < 0.0) return color2;
	if (distance > 2.0) return color1;
	float dd = pow(1.0 - distance / 2.0, sharpness);
	return ((color2 - color1) * dd) + color1;
}

float distanceToEdge(vec3 point){
	float dx = abs(point.x > 0.5 ? 1.0 - point.x : point.x);
	float dy = abs(point.y > 0.5 ? 1.0 - point.y : point.y);
	if (point.x < 0.0) dx = -point.x;
	if (point.x > 1.0) dx = point.x - 1.0;
	if (point.y < 0.0) dy = -point.y;
	if (point.y > 1.0) dy = point.y - 1.0;
	if ((point.x < 0.0 || point.x > 1.0) && (point.y < 0.0 || point.y > 1.0)) return sqrt(dx * dx + dy * dy);
	return min(dx, dy);
}

vec4 seeThrough(float yc, vec2 p, mat3 rotation, mat3 rrotation){
	float hitAngle = mod(180.0 - degrees(acos(yc / radius) - cylinderAngle()), 360.0);
	float hitPoint = hitAngle / 360.0;
	vec3 point = rotation * vec3(p, 1.0);
	point.y = hitPoint;
	point = rrotation * point;
	
	if (yc <= 0.0 && (point.x < 0.0 || point.y < 0.0 || point.x > 1.0 || point.y > 1.0)){
		return vec4(0.0, 0.0, 0.0, 0.0);
	}
	
	if (yc > 0.0) return texture2D(frame, p);
	
	vec4 color = texture2D(frame, vec2(point.x, point.y));
	vec4 tcolor = vec4(0.0, 0.0, 0.0, 0.0);

	return antiAlias(color, tcolor, distanceToEdge(point));
}

vec4 seeThroughWithShadow(float yc, vec2 p, vec3 point, mat3 rotation, mat3 rrotation){
	float shadow = distanceToEdge(point) * 30.0;
	shadow = (1.0 - shadow) / 3.0;
	if (shadow < 0.0) shadow = 0.0;
	else shadow *= amount;

	vec4 shadowColor = seeThrough(yc, p, rotation, rrotation);
	shadowColor.r -= shadow;
	shadowColor.g -= shadow;
	shadowColor.b -= shadow;
	return shadowColor;
}

vec4 backside(float yc, vec3 point){
	vec4 color = texture2D(frame, vec2(point.x, point.y));
	float gray = (color.r + color.b + color.g) / 15.0;
	gray += (8.0 / 10.0) * (pow(1.0 - abs(yc / radius), 2.0 / 10.0) / 2.0 + (5.0 / 10.0));
	color.r = gray;
	color.g = gray;
	color.b = gray;
	return color;
}

vec4 behindSurface(float yc, vec3 point, mat3 rrotation){
	float shado = (1.0 - ((-radius - yc) / amount * 7.0)) / 6.0;
	shado *= 1.0 - abs(point.x - 5.0 / 10.0);
	
	yc = (-radius - radius - yc);
	
	float hitAngle = mod(degrees(acos(yc / radius) + cylinderAngle()) - 180.0, 360.0);			

	float hitPoint = hitAngle / 360.0;
	
	point.y = hitPoint;
	
	point = rrotation * point;
	
	if (yc < 0.0 && point.x >= 0.0 && point.y >= 0.0 && point.x <= 1.0 && point.y <= 1.0 && (hitAngle < 180.0 || amount > 5.0 / 10.0)){
		shado = 1.0 - (sqrt(pow(point.x - (5.0 / 10.0), 2.0) + pow(point.y - (5.0 / 10.0), 2.0)) / (71.0 / 100.0));
		shado *= pow(-yc / radius, 3.0);
		shado *= 5.0 / 10.0;
	} else
		shado = 0.0;
	
	return vec4(0.0, 0.0, 0.0, shado);
}

void main(void) {
	float angle = 30.0;
	float c = cos(radians(-angle));
	float s = sin(radians(-angle));

	mat3 rotation = mat3(
		c, s, 0,
		-s, c, 0,
		0.12, 0.258, 1
	);

	c = cos(radians(angle));
	s = sin(radians(angle));

	mat3 rrotation = mat3(
		c, s, 0,
		-s, c, 0,
		0.15, -0.5, 1
	);

	vec2 p = vec2(gl_FragCoord.x / size.x, gl_FragCoord.y / size.y);
	vec3 point = rotation * vec3(p, 1.0);
	float y = point.y;
	
	float yc = y - cylinderCenter();
	
	if (yc < -radius)
	{
		// Behind surface
		gl_FragColor = behindSurface(yc, point, rrotation);
		return;
	}

	if (yc > radius)
	{
		// Flat surface
		gl_FragColor = texture2D(frame, p);
		return;
	}
	
	float hitAngle = mod(degrees(acos(yc / radius) + cylinderAngle()) - 180.0, 360.0);			

	float hitPoint = hitAngle / 360.0;
	
	point.y = hitPoint;
	
	point = rrotation * point;


	if ((hitAngle > 180.0 && amount < 5.0 / 10.0) || (hitAngle > 90.0 && amount < 0.0))
	{
		gl_FragColor = seeThrough(yc, p, rotation, rrotation);
		return;
	}

	if (point.x < 0.0 || point.y < 0.0 || point.x > 1.0 || point.y > 1.0){
		gl_FragColor = seeThroughWithShadow(yc, p, point, rotation, rrotation);
		return;
	}
	
	vec4 color = backside(yc, point);
	
	vec4 otherColor;
	if (yc < 0.0){
		
		float shado = 1.0 - (sqrt(pow(point.x - 0.5, 2.0) + pow(point.y - 0.5, 2.0)) / 0.71);
		shado *= pow(-yc / radius, 3.0);
		shado *= 0.5;

		otherColor = vec4(0.0, 0.0, 0.0, shado);
	} else {
		otherColor = texture2D(frame, p);
	}
	
	color = antiAlias(color, otherColor, radius - abs(yc));

	//gl_FragColor = antiAlias(color, vec4(1.0, 0.0, 0.0, 1.0), 3.0);

	//gl_FragColor = antiAlias(color, seeThrough(yc, p, rotation, rrotation), 3.0);
	
	vec4 cl = seeThroughWithShadow(yc, p, point, rotation, rrotation);
	float dist = distanceToEdge(point);
	
	gl_FragColor = antiAlias(color, cl, dist);
	
	//gl_FragColor = antiAlias(gl_FragColor, texture2D(frame, p), radius - yc);
	//gl_FragColor = antiAlias(gl_FragColor, vec4(0.0, 0.0, 0.0, 0.0), radius + yc);

}
</script>

</head>
<body>
    <canvas id="canvas" width="640" height="480"></canvas>
    <br/>
    <input type="range" min="0" max="100" value="0" onchange="pageCurl.updateTime(this.value / 100);"/>
</body>
</html>
